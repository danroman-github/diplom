
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ГодОтпусков = Формат(Объект.Период, "ДФ=yyyy");
	Если Год(Объект.Период) = 1 Тогда
		СформироватьСписокВыбораГода(Год(ОбщегоНазначенияКлиент.ДатаСеанса()));
	Иначе
	    СформироватьСписокВыбораГода(Год(Объект.Период));
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ГодОтпусковПриИзменении(Элемент)
	
	ДатаТекстом = "01.01." + ГодОтпусков;
	Объект.Период = СтроковыеФункцииКлиентСервер.СтрокаВДату(ДатаТекстом);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтпускаСотрудников

&НаКлиенте
Процедура ОтпускаСотрудниковДатаНачалаПриИзменении(Элемент)
	ПроверкаДлительностиОтпуска(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОтпускаСотрудниковДатаОкончанияПриИзменении(Элемент)
	ПроверкаДлительностиОтпуска(Ложь);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьАнализГрафика(Команда)
	
	Адрес = ПоместитьДанные();
	ПараметрыФормы = Новый Структура("Адрес", Адрес);
	
	ОткрытьФорму("Документ.ВКМ_ГрафикОтпусков.Форма.АнализГрафика", ПараметрыФормы, Элементы.ОтпускаСотрудников);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СформироватьСписокВыбораГода(Год)
    
    Элементы.ГодОтпусков.СписокВыбора.Очистить();
    Элементы.ГодОтпусков.СписокВыбора.Добавить(Формат(Год-3, "ЧГ=0"));
	Элементы.ГодОтпусков.СписокВыбора.Добавить(Формат(Год-2, "ЧГ=0"));
	Элементы.ГодОтпусков.СписокВыбора.Добавить(Формат(Год-1, "ЧГ=0"));
	Элементы.ГодОтпусков.СписокВыбора.Добавить(Формат(Год, "ЧГ=0"));
    Элементы.ГодОтпусков.СписокВыбора.Добавить(Формат(Год+1, "ЧГ=0")); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаДлительностиОтпуска(Проверка)
	
	ТЧДанные = Элементы.ОтпускаСотрудников.ТекущиеДанные; 
	
	Если Проверка И НЕ ЗначениеЗаполнено(ТЧДанные.ДатаОкончания) Тогда
		ТЧДанные.ДатаОкончания = ТЧДанные.ДатаНачала + 60 * 60 * 24 * 27;
		Возврат;
	ИначеЕсли НЕ Проверка И НЕ ЗначениеЗаполнено(ТЧДанные.ДатаНачала) Тогда 
		ТЧДанные.ДатаНачала = ТЧДанные.ДатаОкончания - 60 * 60 * 24 * 27;
		Возврат;
	КонецЕсли;
	
	ТЧДанные.ВсегоДней = Окр((КонецДня(ТЧДанные.ДатаОкончания) - НачалоДня(ТЧДанные.ДатаНачала)) / (60 * 60 * 24), 0);
	
	ДатаНачала = Формат(ТЧДанные.ДатаОкончания - 60 * 60 * 24 * 27, "ДФ=dd.MM.yyyy");
	ДатаОкончания = Формат(ТЧДанные.ДатаНачала + 60 * 60 * 24 * 27, "ДФ=dd.MM.yyyy");		
		
	Если ТЧДанные.ВсегоДней > 28 Тогда		
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			СтрШаблон("Длительность отпуска более 28 дней, больше на %1.
				| Рекомендуемая дата начала - %2 или окончания %3", ТЧДанные.ВсегоДней - 28, ДатаНачала, ДатаОкончания), 
			, 
			"Объект.ОтпускаСотрудников.ДатаОкончания");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьДанные()
	
	Возврат ПоместитьВоВременноеХранилище(Объект.ОтпускаСотрудников.Выгрузить(, "Сотрудник, ДатаНачала, ДатаОкончания"), УникальныйИдентификатор);
	
КонецФункции

#КонецОбласти
