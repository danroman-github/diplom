
#Область СлужебныеПроцедурыИФункции

Процедура ВКМ_ОтправкаУведомленийТелеграм() Экспорт
	
	Если НЕ ЗначениеЗаполнено(Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить())
		ИЛИ НЕ ЗначениеЗаполнено(Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить()) Тогда
		Возврат;
	КонецЕсли;
	
	УведомленияДляОтправки = Справочники.ВКМ_УведомленияТелеграмБоту.УведомленияДляОтправки();
	
	Для Каждого ЭлементМассива Из УведомленияДляОтправки Цикл
		
		УспешноОтправлено = Истина;
		ОтправитьСообщениеТелеграмБоту(ЭлементМассива.ТекстСообщения, УспешноОтправлено);
		
		Если УспешноОтправлено Тогда
			ОбъектДляУдаления = ЭлементМассива.Ссылка.ПолучитьОбъект();
			ОбъектДляУдаления.Удалить();
		КонецЕсли;
		
	КонецЦикла;

КонецПроцедуры

Процедура ОтправитьСообщениеТелеграмБоту(ПередаваемыйТекст, УспешноОтправлено)
	
	ГруппаID = Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить();
	Токен = Константы.ВКМ_ТокенУправленияТелеграмБотом.Получить();	
	
	АдресРесурса = "bot"+Токен+"/sendMessage";
	
	Соединение = Новый HTTPСоединение("api.telegram.org", 443,,,,,Новый  ЗащищенноеСоединениеOpenSSL());
	
	Данные = Новый Структура;
	Данные.Вставить("chat_id", ГруппаID);
	Данные.Вставить("text", ПередаваемыйТекст);
	
	ЗаписьJSON = Новый ЗаписьJSON;
	ЗаписьJSON.УстановитьСтроку();
	ЗаписатьJSON(ЗаписьJSON, Данные);
	СтрокаЗапроса = ЗаписьJSON.Закрыть();
	
	Заголовки = Новый Соответствие;
	Заголовки.Вставить("Content-type", "application/json");
	
	Запрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
	Запрос.УстановитьТелоИзСтроки(СтрокаЗапроса);
	
	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	
	Если Ответ.КодСостояния <> 200 Тогда
		
		СтрокаОтвета = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()) + ". Код ответа-" + Ответ.КодСостояния;
		ЗаписьЖурналаРегистрации("HTTPСервисы.Ошибка", УровеньЖурналаРегистрации.Ошибка,,, СтрокаОтвета,);
		УспешноОтправлено = Ложь;
		
	КонецЕсли;
	
	ЗаписьЖурналаРегистрации("HTTPСервисы.Отправка", 
		УровеньЖурналаРегистрации.Информация,
		,
		, 
		"Сообщение в телеграм отправлено",);
	
КонецПроцедуры

#КонецОбласти