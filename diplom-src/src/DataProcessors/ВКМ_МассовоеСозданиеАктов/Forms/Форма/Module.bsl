
#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	Объект.СписокРеализаций.Очистить(); 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьДокументы(Команда)
	
	ОчиститьСообщения();
	
	Если НЕ ЗначениеЗаполнено(Объект.Период) Тогда
		ТекстСообщения = "Не заполнен период создания документов.";
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = ПроверитьЗаполнениеКонстант();
	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
    ДлительнаяОперация = ВыполнитьВФонеНаСервере();
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ВыполнитьВФонеЗавершение", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении);
	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПроверитьЗаполнениеКонстант()
	
	ТекстСообщения = "";
	Если НЕ ЗначениеЗаполнено(Константы.ВКМ_НоменклатураАбонентскаяПлата.Получить()) Тогда
		ТекстСообщения = "Не заполнена константа 'НоменклатураАбонентскаяПлата'.";	
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Константы.ВКМ_НоменклатураРаботыСпециалиста.Получить()) Тогда
		
		Если ТекстСообщения = "" Тогда
			ТекстСообщения = "Не заполнена константа 'Номенклатура Работы специалиста'.";
		Иначе
			НовыйТекстСообщения = "Не заполнена константа Номенклатура Работы специалиста.";
			МассивСтрок = Новый Массив;
			МассивСтрок.Добавить(ТекстСообщения);
			МассивСтрок.Добавить(НовыйТекстСообщения);
			ТекстСообщения = СтрСоединить(МассивСтрок, Символы.ПС);
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ТекстСообщения;
	
КонецФункции

&НаСервере
Функция ВыполнитьВФонеНаСервере()
	
	ИмяМетода = "Обработки.ВКМ_МассовоеСозданиеАктов.СоздатьСписокНаСервере";
	
	Возврат ДлительныеОперации.ВыполнитьФункцию(УникальныйИдентификатор, ИмяМетода, Объект.Период.ДатаНачала, Объект.Период.ДатаОкончания);
	
КонецФункции 

&НаКлиенте
Процедура ВыполнитьВФонеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	СписокДокументовМассив = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
	ЗаполнитьСписокРеализаций(СписокДокументовМассив);
	
КонецПроцедуры 

&НаКлиенте
Процедура ЗаполнитьСписокРеализаций(СписокДокументовМассив)
	
	Объект.СписокРеализаций.Очистить();
	
	Для Каждого СписокРеализацийСтруктура Из СписокДокументовМассив Цикл
		
		НоваяСтрока = Объект.СписокРеализаций.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СписокРеализацийСтруктура);
		
		Если СписокРеализацийСтруктура.Реализация = Неопределено Тогда
			ТекстСообщения = СтрШаблон("По договору '%1' не удалось создать документ. Подробности в журнале регистрации.", НоваяСтрока.Договор);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);	
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти